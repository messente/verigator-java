buildscript {
    repositories() {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}


apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'com.messente.verigator'
version '1.1'

sourceCompatibility = 1.8

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        repo = 'verigator'
        name = 'com.messente.verigator'
        userOrg = "messente"
        version {
            name = version
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name:'slf4j-simple', version: '1.7.25'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mock-server', name: 'mockserver-netty', version: '3.9.1'
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            from components.java
            groupId 'com.messente.verigator'
            artifactId 'verigator-java'
            version version
        }
    }
}

check.dependsOn jacocoTestReport
